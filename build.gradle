buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "com.moowork.node" version "1.2.0"
	id "org.asciidoctor.convert" version "1.5.6"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.saskcow'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = file('node_modules/webpack/bin/webpack.js')
}

task nightwatchSetup(type: NpmTask) {
    args = ['run', 'nightwatch-setup']
}

task nightwatch(type: NpmTask, dependsOn: 'nightwatchSetup') {
    args = ['run', 'nightwatch']
}

ext {
    snippetsDir = file('./reports/java-snippets')
}

ext['spring-restdocs.version']='1.2.3.RELEASE'

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-freemarker')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('com.h2database:h2')
	compileOnly('org.projectlombok:lombok')
    asciidoctor('org.springframework.restdocs:spring-restdocs-asciidoctor:1.2.3.RELEASE')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.2.3.RELEASE')
    testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	testCompile('org.assertj:assertj-core:3.9.0')
}

processResources.dependsOn 'webpack'

clean.delete << file('src/main/webapp/dist')
